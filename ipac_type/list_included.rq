PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ipac: <http://ipac.mic.gov.it/>

SELECT DISTINCT ?principalType WHERE {
  ?s rdf:type ?principalType .

  FILTER NOT EXISTS {
    ?s rdf:type ?ontType .
    FILTER(?ontType IN (
      owl:Class,
      rdfs:Class,
      rdf:Property,
      owl:ObjectProperty,
      owl:DatatypeProperty,
      owl:AnnotationProperty,
      owl:Ontology,
      owl:Restriction,
      rdfs:Datatype,
      owl:NamedIndividual  # Optional
    ))
  }
  FILTER (
    !STRSTARTS(STR(?s), STR(xsd:)) &&
    !STRSTARTS(STR(?s), "http://www.openlinksw.com/virtrdf-data-formats#") &&
    !STRSTARTS(STR(?s), "http://www.openlinksw.com/schemas/virtrdf#") &&
    !STRSTARTS(STR(?s), "http://www.w3.org/ns/sparql-service-description#") &&
    !STRSTARTS(STR(?principalType), "http://www.w3.org/ns/sparql-service-description#")
  )

  FILTER NOT EXISTS {
    ?s rdf:type ?broaderType .
    ?principalType rdfs:subClassOf+ ?broaderType .
    FILTER (?principalType != ?broaderType)
  }
}
LIMIT 10000